mkrqr1	; mkr 06/22/2016; kriah (קריה) and trope (טעמים) - HTML output
	; ^mkrqr1 handles the creation of the html output from the ^torah
	;   global. See ^mkrqr0 for the structure of the ^torah global.
	;
writetorah0	; write out entire Chumash, with sidros
	; mkr 01/23/20 - write out overall pesukim, full posuk format, html with tags and such
	;  - for each sidra
	; long form for entire Chumash, but breaking it up by sidra
	n chars,fil,glb0,glb1,ii,jend,jstart,nm,sefer,seferii,sefer1,sefer2,
		sidraf,sidranm,wrds
	s glb0="^torah",glb1="^TMP("_$j_",""out"")"
	k @glb1
	m chars=^mkr4("chars") ; details about various Hebrew unicode characters
	m sefarim=^mkr4("sefer") ; details about various books of Tanach
	m sidros=^mkr4("sidros") ; details about various parshiyos
	m wrds=^mkr2("wrds") ; some useful Hebrew words
	d writehdr($$zconvert^mkrutl(wrds("chumash"),"O","UTF8"))
	;
	s ii=0 ; starting chumash
	f sidcnt=1:1 q:'$d(sidros(sidcnt))  d
	. s firstsidra=0
	. k sidra m sidra=sidros(sidcnt)
	. f x="ch0","chL","v0","vL","nm","nmEng","sefer" s @x=sidra(x)
	. i ii'=sefer d  ; starting a new sefer (chumash)
	.. s firstsidra=1 ; this is the first sidra from that chumash
	.. i ii d writeend2 ; end of previous sefer
	.. s ii=sefer ; number of current sefer
	.. m seferii=sefarim(ii),sefernm=seferii
	.. s sefer1=$$zconvert^mkrutl(wrds("sefer")_" "_sefernm,"O","UTF8")
	.. d writehdr2(sefer1,"","") ; header for this chumash
	. s sidranm=$$zconvert^mkrutl(wrds("parshas")_" "_nm,"O","UTF8")
	. s jstart=^torah("cvE",ii,ch0,v0) ; first verse counted from beginning of sefer
	. s jend=^torah("cvE",ii,chL,vL) ; last verse
	. d writehdr2("",sidranm,firstsidra)
	. d writesidros1(ii,jstart,jend) ; actual pesukim
	d writeend2 ; end of this last sefer
	d writeend ; close html
	d writeout(,glb1,"Chumash","outAll")
	k @glb1
	q
	;
writetorah35	;
	; write out overall pesukim, full posuk format, html with tags and such
	; one sefer at a time
	n chars,fil,glb0,glb1,ii,sefer,sefer1,sefer2
	s glb0="^torah",glb1="^TMP("_$j_",""out"")"
	k @glb1
	m chars=^mkr4("chars") ; details about various Hebrew unicode characters
	f ii=1:1:35 d
	. m sefer=^mkr4("sefer",ii)
	. s sefer1=$$zconvert^mkrutl(sefer,"O","UTF8")
	. d writehdr(sefer1),writehdr2(sefer1,"")
	. d writetorah1
	. d writeend2,writeend
	. d writeout(ii,glb1,sefer("eng"),"out")
	. k @glb1
	; also write out the sidros in the Chumash
	;d writesidros
	; okay, now ta'am elyon
	;d writeelyon
	q
	;
writehdr(sefer1,sidra)	; sidra if there is one
	n j,x,y
	s sidra=$g(sidra)
	; some html header stuff
	f j=1:1 s x=$t(leadtxt+j) q:x[";;;"  s y=$p(x,";",3,99) d add(y)
	s name=$s(sidra'="":sidra,1:sefer1)
	d add("<title>"_name_"</title>")
	d add("</head>")
	d add("<body>")
	q
	;
writehdr2(sefer1,sidranm,firstsidra)	; more header for regular
	n class0
	s sidranm=$g(sidranm) ; sidra
	s firstsidra=$g(firstsidra)
	i $g(sefer1)'="" d
	. d add("<div class='sefer'>")
	. d outsefernm(sefer1)
	s class0=$s(firstsidra:"first",1:"")
	i $g(sidranm)'="" d
	. d outsidranm(sidranm,class0) ; sidra if there
	q
outsefernm(sefer,class0)	; write out sefer name
	n class
	d addclass(.class0,"sefernm ivr")
	d add("<div class='"_class0_"'>"_sefer_"</div>")
	q
outsidranm(sidra,class0)	; write out parsha name
	d addclass(.class0,"sidranm ivr")
	d add("<div class='"_class0_"'> "_sidra_"</div>")
	q
	;
addclass(class0,class1)	; add on to class0, passed by reference
	s class0=$g(class0)
	s:class0'="" class0=class0_" "
	s class0=class0_class1
	q
	;
writeend2	;
	d add("</div>")
	q
writeend	;
	d add("</body>")
	d add("</html>")
	q
	;
writetorah1	; single sefer
	n fil,j,posuk,shiraf,shiratoggle,x,y
	s shiraf="noshira",shiratoggle=0 ; we are not in a shira - we'll toggle
	d add("<div class='"_shiraf_"'>")
	; now go through the pesukim
	s j=0 f  s j=$o(@glb0@(ii,j)) q:j=""  q:j'?1.N  d writetorah2(glb0)
	d add("</div>") ; end of shira/noshira section
	q
	;
writetorah2(glb0,seferf,out1)	; single posuk including outside framework
	; --UTILITY-- also called by writequery 
	; most variables from local symbol table, like which verse,
	;  but we pass in glb0 because
	;  for finding ta'am elyon, tachton, we need to write out first from
	;  ta'am elyon in one global node, then ta'am tachton in another.
	; out1 is array (sometimes) passed by writequery - particular words to highlight
	s seferf=$g(seferf) ; do we need to include the sefer
	s chap=@glb0@(ii,j,"chap"),verse=@glb0@(ii,j,"verse")
	d add("<span class='posuk'>")
	s posuk=$$writeposuk(ii,j,seferf,.out1)
	d add($$zconvert^mkrutl(posuk,"O","UTF8"))
	d add("</span>") ; end of posuk
	i shiratoggle d  ; we hit a {ש} in the posuk
	. s shiratoggle=0
	. d add("</div>")
	. s shiraf=$s(shiraf="noshira":"shira",shiraf="shira":"noshira",1:"error")
	. d add("<div class='"_shiraf_"'>")
	q
	;
leadtxt	;
	;;<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
	;;<HTML LANG=HE DIR=RTL>
	;;<HEAD>
	;;<META HTTP-EQUIV="Content-Type" CONTENT="text/html; CHARSET=utf-8">
	;;<!--Copyright 2012 Mechon Mamre, 12 Hayyim Vital, Jerusalem-->
	;;<LINK REL="stylesheet" HREF="includes\c.css" TYPE="text/css">
	;;;
	q
	;
writeposuk(i,j,seferf,out1)	; write one single posuk, built from its words
	; if seferf, add sefer
	; if out1 array, highlight certain words
	n chap,crlf,glb0,highlightf,k,ln,lvls,nbsp,nbsp7,psk,posuk,sefernm,sp,verse
	s glb0="^torah"
	s sp=$c(32),nbsp=$c(160),crlf="<br/>"
	s thinsp=$c(8203) ; actually "ZERO WIDTH NON-JOINER"
	s nbsp7=nbsp_nbsp_nbsp_nbsp_nbsp_nbsp_nbsp ; half of אשר אשר אשר
	s sefernm="" i $g(seferf) s sefernm=@glb0@(i,"sefer")_" "
	m psk=@glb0@(i,j) ; whole posuk, also subnode for each word and other stuff
	d gettropelvls^mkrqr0(.psk,.lvls) ; build tree of trope levels
	s chap=psk("chap"),verse=psk("verse")
	s posuk=sefernm_"<span class='chapverse'>"_chap_"("_verse_")</span>" ; begin building output
	s ln=psk(0) ; number of words
	f k=1:1:ln s highlightf=$d(out1(k)) s posuk=posuk_$$writewrd(i,j,k,.psk,highlightf)
	q posuk
writewrd(i,j,k,psk,highlightf)	; single word
	n brk,brks,kk,kk2,ksiv,lvl,realmend,realmstart,word,wrd
	s word=""
	; space between words - we won't need it if there are box borders in between
	m realmstart=psk(k,"realmstart") ; realm-start spans
	i k>1 s word=$s($d(realmstart):thinsp,1:sp)
	m realmstart=psk(k,"realmstart") ; realm-start spans
	f lvl=1:1:4 i $d(realmstart(lvl)) d
	.; special for lvl 1: first section of the posuk will look a little
	.;  different, with no border on the right (next to the chap(verse) marking)
	. i lvl=1,'$d(psk(k-1,"realmend")) s word=word_"<span class='lvl_"_lvl_" first'>" q
	. s word=word_"<span class='lvl_"_lvl_"'>"
	m wrd=psk(k) ; actual word
	m brks=psk("brks")
	s kk=$o(brks(k_",")) ; is there a break {R} in the _middle_ of the word
	i $p(kk,",")=k s brk=$o(brks(kk,"")) d
	. s kk2=$p(kk,",",2)
	. i brk="R" s wrd=$e(wrd,1,kk2)_crlf_$e(wrd,kk2+1,99)
	i highlightf s wrd="<span class='highlight'>"_wrd_"</span>"
	s word=word_wrd
	i $d(wrd("ksiv")) s ksiv=wrd("ksiv") s word=word_sp_"["_ksiv_"]"
	m realmend=psk(k,"realmend") ; realm-end closing spans
	f lvl=1:1:4 i $d(realmend(lvl)) s word=word_"</span>"
	i $d(brks(k)) s brk="" f  s brk=$o(brks(k,brk)) q:brk=""  d
	. s word=word_sp
	. i brk="S" s word=word_nbsp7_"ס"_nbsp7
	. i brk="P" s word=word_nbsp7_"פ"_crlf
	. i brk="R" s word=word_crlf
	. i brk="SH" d
	.. s word=word_crlf_crlf
	..; something else here: this is either the beginning or end of shirah,
	..;  or the end of the whole chumash. toggle shira flag
	.. s shiratoggle=1
	q word
	;
add(x)	; add one row to output global
	n glb1
	s glb1="^TMP("_$j_",""out"")"
	s (cnt,@glb1@(0))=$g(@glb1@(0))+1
	s @glb1@(cnt)=x
	q
	;
writeout(scnt,glb1,name,fldr,suffix)	; to host file
	; works in either Cache' or gt.m
	; glb1 should be in format glb1(0)=# of rows, glb1(1-#) are the rows
	; the rows may be, say tab-delimited
	n fil,i,j,num,sl,scnttxt
	s sl=$$slash^mkrutl() ; / or \
	s scnt=$g(scnt) ; if we're including a count 1,2,3 in the filename
	s scnttxt="" s:scnt scnttxt=scnt_". "
	s fldr=$g(fldr) q:fldr=""  ;s:fldr="" fldr="out" ; default
	s:$g(suffix)="" suffix="htm"
	s fil=$$getPath^mkrutl()_"QR"_sl_fldr_sl_scnttxt_name_"."_suffix
	d openfil^mkrutl(fil,"O",1,,1,1)	;
	i '$t w !,"Couldn't open "_fil q
	use fil
	f j=1:1:@glb1@(0) d
	. w !,@glb1@(j) 
	close fil
	q
	;
writesidros	;
	; write out overall pesukim, full posuk format, html with tags and such
	;  - for each sidra
	; mkr 01/23/20 - add long form for entire Chumash, but breaking it up by sidra
	n chars,ch0,chL,chars,ii,sidcnt,jend,jstart,
		nm,nmEng,sefer,sefer1,sidros,v0,vL
	s glb0="^torah",glb1="^TMP("_$j_",""out"")"
	k @glb1
	m chars=^mkr4("chars") ; details about various Hebrew unicode characters
	m sidros=^mkr1("sidros") ; details about various parshiyos
	f sidcnt=1:1 q:'$d(sidros(sidcnt))  d
	. k sidra m sidra=sidros(sidcnt)
	. f x="ch0","chL","v0","vL","nm","nmEng","sefer" s @x=sidra(x)
	. s ii=sefer ; number of sefer
	. s jstart=^torah("cvE",ii,ch0,v0) ; first verse counted from beginning of sefer
	. s jend=^torah("cvE",ii,chL,vL) ; last verse
	. m sefer=^mkr4("sefer",ii)
	. s sefer1=$$zconvert^mkrutl(sefer,"O","UTF8")
	. d writehdr(sefer1,nmEng),writehdr2(sefer1,nmEng)
	. d writesidros1(ii,jstart,jend) ; actual pesukim
	. d writeend2
	. d writeend
	. d writeout(sidcnt,glb1,nmEng,"outSidros")
	. k @glb1
	q
writesidros1(i,jstart,jend)	; single sidra
	n shiraf
	s shiraf="noshira",shiratoggle=0 ; we are not in a shira - we'll toggle
	d add("<div class='"_shiraf_"'>")
	; now just go through the pesukim
	s j=(jstart-1) f  s j=$o(@glb0@(i,j)) q:j=""  q:j'?1.N  q:j>jend  d writetorah2(glb0)
	d add("</div>") ; end of shira/noshira section
	q
	;
writequery(out,title,subtitle,filenm)	; write out html from query result (usually from ^mkrqr3)
	; write out overall pesukim, full posuk format, html with tags and such.
	; input is array out(i,j) containing pesukim we want.
	; since the results may be in more than one sefer, we need to keep track of that.
	; Also, if out(i,j,k) is passed, means a particular word is marked, so highlight it.
	n chars,glb0,glb1
	s glb0="^torah",glb1="^TMP("_$j_",""out"")"
	k @glb1
	m chars=^mkr4("chars") ; details about various Hebrew unicode characters
	d writehdr(title,subtitle),writehdr2(title,subtitle)
	d writequery1(.out) ; actual pesukim
	d writeend2,writeend
	d writeout(,glb1,filenm,"outQueries")
	k @glb1
	q
writequery1(out)	; write out pesukim
	n i,j,out1,seferf,shiraf
	s seferf=1 ; write out sefer with chap and verse
	s shiraf="noshira",shiratoggle=0 ; we are not in a shira - we'll toggle
	d add("<div class='"_shiraf_"'>")
	; now go through the pesukim
	s i="" f  s i=$o(out(i)) q:i=""  d
	. s j="" f  s j=$o(out(i,j)) q:j=""  d
	.. d add("<div>")
	.. k out1 m out1=out(i,j) ; if any particular words marked, in out(i,j,k)
	.. d writetorah2(glb0,seferf,.out1)
	.. d add("</div>")
	d add("</div>") ; end of shira/noshira section
	q
	;
writeelyon	; various versions of ta'am elyon/tachton
	n glb00,j1,j2,shiraf,shiratoggle
	s glb0="^torah(""elyon"")"
	s glb00="^torah" ; for finding ta'am tachton in its place
	;
	m sefer=@glb0@("sefer")
	s sefer1=$$zconvert^mkrutl(sefer,"O","UTF8")
	;
	; we have several versions, so do several times
	; first, just ta'am elyon by itself
	;
	d writehdr(sefer1),writehdr2(sefer1) ; just once here - all one document
	s i="" f  s i=$o(@glb0@(i)) q:i'?1.N  d
	. s sefer2=@glb0@(i,"sefer") ; individual chumashim inside document
	. s sefer2=$$zconvert^mkrutl(sefer2,"O","UTF8")
	. d add("<h3 class='ivr'>"_sefer2_"</h3>")
	. d writetorah1
	d writeend2,writeend
	d writeout("elyon",glb1,sefer("eng"),"out")
	k @glb1
	;
	; now left-right version
	s shiraf="noshira",shiratoggle=0 ; we are not in a shira - we'll toggle
	;
	d writehdr(sefer1),writehdr3 ; partially different header
	s i="" f  s i=$o(@glb0@(i)) q:i'?1.N  d  ; several different sefarim have this
	. s sefer2=@glb0@(i,"sefer") ; individual chumashim inside document
	. s sefer2=$$zconvert^mkrutl(sefer2,"O","UTF8")
	. d add("<tr>")
	. d add("<td colspan='2' class='sefernm'>"_sefer2_"</td>")
	. d add("</tr>")
	. d add("<tr class='pesukim'>")
	.; now go through pesukim
	.; we need to go through both ta'am elyon, and the corresponding ta'am tachton
	.; (which is stored elsewhere)
	. d add("<td>") ; contains the entire set of pesukim from that sefer
	. s j=0 f  s j=$o(@glb0@(i,j)) q:j=""  q:j'?1.N  d
	.. d writetorah2(glb0)
	. d add("</td>")
	.; now ta'am tachton - use different global node to find pesukim
	.; chapter # should be okay, but pesukim are number differently
	. d add("<td>") ; second set of pesukim from that sefer
	. s j1=$g(@glb00@(i,"tachton","start")),j2=$g(@glb00@(i,"tachton","end"))
	. i j1 f j=j1:1:j2 d writetorah2(glb00)
	. d add("</td>")
	. d add("</tr>") ; end of two sets of pesukim from that sefer
	d add("</tbody>")
	d add("</table>")
	d writeend2,writeend
	d writeout("elyonLR",glb1,sefer("eng")_"LR","out")
	k @glb1
	q
writehdr3	; more header for ta'am elyon/tachton Left Right
	; some html header stuff
	n nm1,nm2,j,x,y
	f j=1:1 s x=$t(leadtxt2+j) q:x[";;;"  s y=$p(x,";",3,99) d add(y)
	s nm1=$$zconvert^mkrutl("טעם עליון","O","UTF8")
	s nm2=$$zconvert^mkrutl("טעם תחתון","O","UTF8")
	d add("<th class='colhdr'>"_nm1_"</th>")
	d add("<th class='colhdr'>"_nm2_"</th>")
	f j=1:1 s x=$t(leadtxt3+j) q:x[";;;"  s y=$p(x,";",3,99) d add(y)
	q
	;
leadtxt2	; for ta'am elyon/tachtonLeftRight
	;;<div>
	;;<table id='elyontachton' class='sefer2 noshira ivr'>
	;;<thead>
	;;<tr>
	;;;
	q
leadtxt3	; more for ta'am elyon/tachtonLeftRight
	;;</tr>
	;;</thead>
	;;<tbody>
	;;;
	q
	;
